(int) handle_owner_messages() impure inline {
    slice in_msg_body = ctx.at(BODY);

    if ctx.at(OPCODE) == op::ft::transfer {
        int jetton_amount = in_msg_body~load_coins();
        slice to_owner_address = in_msg_body~load_msg_addr();

        slice response_address = in_msg_body~load_msg_addr(); 
        _ = in_msg_body~load_dict();        ;; skip custom_payload
        int fwd_amount = in_msg_body~load_coins();     
        
        ;; payload is std jetton transfer payload
        slice either_forward_payload = in_msg_body;     ;; send transfer payload forward

        storage::balance -= jetton_amount;
        throw_unless(error::low_balance, storage::balance >= 0);

        storage::save();
    
        ;; reserve (original_balance + storage_fees) - jetton_amount
        raw_reserve(jetton_amount - storage_fees(), BALANCE_DECREASED + BALANCE_INCREASED);

        if (~ response_address.address::is_type(ADDR_TYPE_STD)) 
            | response_address.equal_slices(to_owner_address) {
            ;; no separate excess sending
            ;; need to carry remaining_gas + jetton_amount
            ;; storage fee paid by user

            ;; in case of pton-to-pton msg, it try to refund to 
            ;; original sender here since it doesn't know any other address
            msgs::send_nobounce(
                0, 
                to_owner_address, 
                any::ton_transfer(
                    jetton_amount, 
                    ctx.at(SENDER), 
                    either_forward_payload
                ), 
                CARRY_ALL_BALANCE | BOUNCE_IF_FAIL
            );
        } else {
            ;; send exactly jetton_amount + fwd_gas to target address
            ;; carry remaining_gas to response_address 

            ;; in case of pton-to-pton msg, it try to refund to 
            ;; response address here since it's set
            msgs::send_nobounce(
                jetton_amount + fwd_amount, 
                to_owner_address, 
                any::ton_transfer(
                    jetton_amount, 
                    response_address, 
                    either_forward_payload
                ), 
                PAID_EXTERNALLY | BOUNCE_IF_FAIL
            );

            utils::send_excesses(0, response_address, QCARRY_ALL_BALANCE);
        }

        return (true);
    }

    if ctx.at(OPCODE) == op::reset_gas {
        ;; reset gas to current tokenized ton balance + min storage reserve
        reserves::exact(storage::balance + storage_fee::wallet); 
        utils::send_excesses(0, storage::owner_address, CARRY_ALL_BALANCE);
        return (true);
    }

    return (false);
}

